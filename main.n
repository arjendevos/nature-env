import fs
import fmt
import libc
import strings
import syscall
import "parser.n" as *
import "utils.n" as *

const DOUBLE_QUOTE_SPECIAL_CHARS = '\\\n\r"!$`'

// unmarshal reads an env string and returns a map of keys and values.
fn unmarshal(string src):{string:string}! {
    return unmarshal_bytes(src)
}

// unmarshal_bytes parses env file content, returning a map of keys and values.
fn unmarshal_bytes(string src):{string:string}! {
    {string:string} out = {}
    parse_bytes(src, out)
    return out
}

// read reads all env vars from the specified files and returns them as a map
// without loading them into the process environment.
// If no filenames given, defaults to ".env".
fn read(...[string] filenames):{string:string}! {
    var fnames = filenames_or_default(filenames)
    {string:string} env_map = {}

    for fname in fnames {
        var individual = read_file(fname)
        for k, v in individual {
            env_map[k] = v
        }
    }

    return env_map
}

// load reads env file(s) and loads them into the process environment.
// It WILL NOT override env variables that already exist.
// If no filenames given, defaults to ".env".
// Panics on failure. Use try_load for error handling.
fn load(...[string] filenames):void {
    var fnames = filenames_or_default(filenames)
    for fname in fnames {
        load_file(fname, false) catch err {
            panic(fmt.sprintf('failed to load env file "%s": %s', fname, err.msg()))
        }
    }
}

// try_load is like load but returns an error instead of panicking.
fn try_load(...[string] filenames):void! {
    var fnames = filenames_or_default(filenames)
    for fname in fnames {
        load_file(fname, false)
    }
}

// overload reads env file(s) and loads them into the process environment.
// It WILL override env variables that already exist.
// If no filenames given, defaults to ".env".
// Panics on failure. Use try_overload for error handling.
fn overload(...[string] filenames):void {
    var fnames = filenames_or_default(filenames)
    for fname in fnames {
        load_file(fname, true) catch err {
            panic(fmt.sprintf('failed to load env file "%s": %s', fname, err.msg()))
        }
    }
}

// try_overload is like overload but returns an error instead of panicking.
fn try_overload(...[string] filenames):void! {
    var fnames = filenames_or_default(filenames)
    for fname in fnames {
        load_file(fname, true)
    }
}

// write serializes the given environment map and writes it to a file.
// Each line is in the format: KEY="VALUE" (with escaping) or KEY=123 for integers.
fn write({string:string} env_map, string filename):void! {
    var content = marshal(env_map) + '\n'
    var f = fs.open(filename, syscall.O_WRONLY | syscall.O_CREAT | syscall.O_TRUNC, 0o644)
    f.write(content as [u8])
    f.close()
}

// marshal outputs the given environment map as a dotenv-formatted string.
// Each line is in the format: KEY="VALUE" where VALUE is backslash-escaped,
// or KEY=VALUE for pure integer values.
fn marshal({string:string} env_map):string {
    [string] lines = []

    for k, v in env_map {
        if is_integer(v) {
            lines.push(k + '=' + v)
        } else {
            lines.push(k + '="' + double_quote_escape(v) + '"')
        }
    }

    sort_strings(lines)
    return join_strings(lines, '\n')
}


