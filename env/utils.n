import libc

#local
fn get_raw(string key):string! {
    var val = libc.getenv(key.to_cstr())
    if val as anyptr == 0 as anyptr {
        throw errorf('environment variable "%s" is not set', key)
    }
    return val.to_string()
}

#local
fn parse_bool(string s):bool! {
    var lower = to_lower(s)
    if lower == 'true' || lower == '1' || lower == 'yes' {
        return true
    }
    if lower == 'false' || lower == '0' || lower == 'no' {
        return false
    }
    throw errorf('cannot parse "%s" as bool (expected true/false/1/0/yes/no)', s)
}

#local
fn parse_dict(string s):{string:string}! {
    {string:string} result = {}
    var pairs = split_and_trim(s, ','[0])
    for int i = 0; i < pairs.len(); i += 1 {
        var pair = pairs[i]
        if pair.len() == 0 {
            continue
        }
        var eq_pos = -1
        for int j = 0; j < pair.len(); j += 1 {
            if pair[j] == '='[0] {
                eq_pos = j
                break
            }
        }
        if eq_pos < 0 {
            throw errorf('invalid key=value pair: "%s"', pair)
        }
        var k = trim_spaces(pair[0..eq_pos])
        var v = trim_spaces(pair[eq_pos + 1..])
        result[k] = v
    }
    return result
}

#local
fn to_lower(string s):string {
    [u8] result = []
    for int i = 0; i < s.len(); i += 1 {
        var c = s[i]
        if c >= 65 && c <= 90 {
            result.push(c + 32)
        } else {
            result.push(c)
        }
    }
    return result as string
}

#local
fn split_and_trim(string s, u8 delim):[string] {
    [string] parts = []
    var start = 0
    for int i = 0; i < s.len(); i += 1 {
        if s[i] == delim {
            parts.push(trim_spaces(s[start..i]))
            start = i + 1
        }
    }
    parts.push(trim_spaces(s[start..]))
    return parts
}

#local
fn is_space(u8 c):bool {
    return c == ' '[0] || c == '\t'[0] || c == '\r'[0] || c == 11 || c == 12
}

#local
fn trim_spaces(string s):string {
    var left = 0
    for left < s.len() && is_space(s[left]) {
        left += 1
    }
    var right = s.len()
    for right > left && (is_space(s[right - 1]) || s[right - 1] == '\n'[0]) {
        right -= 1
    }
    if left == 0 && right == s.len() {
        return s
    }
    return s[left..right]
}
